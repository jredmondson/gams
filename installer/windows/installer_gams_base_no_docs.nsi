; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "GAMS"

!define PRODUCT_PUBLISHER "James Edmondson"
!define PRODUCT_WEB_SITE "http://jredmondson.github.io/gams/"

!define MADARA_WEB_SITE "http://madara.sourceforge.net/"
!define ACE_WEB_SITE "http://www.cs.wustl.edu/~schmidt/ACE.html"
  
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define env_hklm 'HKLM "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"'
!define env_hkcu 'HKCU "Environment"'

; MUI 1.67 compatible ------
!include "MUI.nsh"
!include "winmessages.nsh"
!include "WordFunc.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "$%GAMS_ROOT%\LICENSE.txt"
; Components page
!insertmacro MUI_PAGE_COMPONENTS
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\GAMS-README.txt"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"
!insertmacro WordAdd
!insertmacro un.WordAdd
; MUI end ------


RequestExecutionLevel admin


; Get the GAMS version
!system "get_version.exe"
!include "VERSION.txt"

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "gams_${PRODUCT_VERSION}_installer.exe"



InstallDir "\gams"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails hide
ShowUnInstDetails hide

Section "debug-libs" SEC01
  SetOutPath "$INSTDIR\lib"
  SetOverwrite ifnewer
  
  File "$%ACE_ROOT%\lib\ACEd.dll"
  File "$%ACE_ROOT%\lib\ACEd.lib"
  
  File "$%MADARA_ROOT%\lib\MADARAd.dll"
  File "$%MADARA_ROOT%\lib\MADARAd.lib"
  
  File "$%GAMS_ROOT%\lib\GAMSd.dll"
  File "$%GAMS_ROOT%\lib\GAMSd.lib"
SectionEnd

Section "-release-libs" SEC02
  SetOutPath "$INSTDIR\lib"
  SetOverwrite ifnewer
  File "$%ACE_ROOT%\lib\ACE.dll"
  File "$%ACE_ROOT%\lib\ACE.lib"
  
  File "$%MADARA_ROOT%\lib\MADARA.dll"
  File "$%MADARA_ROOT%\lib\MADARA.lib"
  
  File "$%GAMS_ROOT%\lib\GAMS.dll"
  File "$%GAMS_ROOT%\lib\GAMS.lib"
SectionEnd

Section "tests" SEC04
  SetOutPath "$INSTDIR\bin"
  File "$%MADARA_ROOT%\bin\profile_architecture.exe"
  File /r "$%MADARA_ROOT%\bin\test*.exe"
  File "$%MADARA_ROOT%\bin\network_profiler.exe"
SectionEnd

Section "-scripts" SEC05
  SetOutPath "$INSTDIR"
  
  File /r "$%GAMS_ROOT%\scripts"
SectionEnd


Section "-include" SEC07
  SetOutPath "$INSTDIR\src"
  
  ; copy the ace, madara and gams source directories
  File /r /x *.svn /x *.obj /x *.sln /x *.sdf /x *.vcxproj /x *.filters /x *.user /x *.log /x *.tlog /x *.pdb /x Static_Debug /x Static_Release /x Debug /x Release "$%ACE_ROOT%\ace"
  File /r /x *.obj /x *.sln /x *.sdf /x *.vcxproj /x *.filters /x *.user /x *.log /x *.tlog /x *.pdb /x Static_Debug /x Static_Release /x Debug /x Release "$%MADARA_ROOT%\include\madara"
  File /r /x *.obj /x *.sln /x *.sdf /x *.vcxproj /x *.filters /x *.user /x *.log /x *.tlog /x *.pdb /x Static_Debug /x Static_Release /x Debug /x Release "$%GAMS_ROOT%\src\gams"
  
  ; copy the using_* files within GAMS_ROOT
  SetOutPath "$INSTDIR"
  
  File /r "$%GAMS_ROOT%\using_*.mpb"
  
  
SectionEnd

Section "-vcredist" SEC11

  SetOutPath "$INSTDIR\vcredist"
  File "redist\vcredist_x64.exe"
  File "redist\vcredist_x86.exe"

  # 64-bit Installs

  # Make sure the variable is clear
  StrCpy $1 ""
  SetRegview 64

  # Detect if it is installed already
  ReadRegStr $1 HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\{1D8E6291-B0D5-35EC-8441-6616F567A0F7}" DisplayName
  
  
  StrCmp $1 "" sp164_not_exists sp164_exists

  sp164_not_exists:
    # From http://blogs.msdn.com/astebner/archive/2007/02/07/update-regarding-silent-install-of-the-vc-8-0-runtime-vcredist-packages.aspx
    # “qb!” for progress with no cancel, “qb” for progress and cancel, “qn” for no interaction

    DetailPrint "Installing VC 13 64-bit Redistributable."  

    ExecWait '$INSTDIR\vcredist\vcredist_x64.exe /q' $0 # Only progress bar
    DetailPrint "vcredist_x64 SP1 Update returned $0"
    

  sp164_exists:
  # Nothing to do

  # 32-bit Installs
  # Make sure the variable is clear
  StrCpy $1 ""
  SetRegview 32

  # Detect if it is installed already
  ReadRegStr $1 HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\{F0C3E5D1-1ADE-321E-8167-68EF0DE699A5}" DisplayName
  StrCmp $1 "" sp1_not_exists sp1_exists

  sp1_not_exists:
    # From http://blogs.msdn.com/astebner/archive/2007/02/07/update-regarding-silent-install-of-the-vc-8-0-runtime-vcredist-packages.aspx
    # “qb!” for progress with no cancel, “qb” for progress and cancel, “qn” for no interaction
    DetailPrint "Installing VC 13 32-bit Redistributable."  
    ExecWait '$INSTDIR\vcredist\vcredist_x86.exe /q' $0 # Only progress bar
    DetailPrint "vcredist_x86 SP1 Update returned $0"

  sp1_exists:
  # Nothing to do

SectionEnd

Section "-tests"
  SetOutPath "$INSTDIR\tests\madara"
  File /r "$%MADARA_ROOT%\tests\*"
  File "$%MADARA_ROOT%\tests\*.cpp"
  
  SetOutPath "$INSTDIR\tests\gams"
  File /r "$%GAMS_ROOT%\tests\*"
  File "$%GAMS_ROOT%\tests\*.cpp"
SectionEnd

Section "-tutorials"
  SetOutPath "$INSTDIR"
  File /r "$%MADARA_ROOT%\tutorials"
SectionEnd

Section "-exes" SEC09
  SetOutPath "$INSTDIR"

  File /r /x *.svn "$%ACE_ROOT%\MPC"

  SetOutPath "$INSTDIR\bin"
  
  File /r /x *.svn "$%ACE_ROOT%\bin\MakeProjectCreator"
  File "$%ACE_ROOT%\bin\mwc.pl"
  File "$%ACE_ROOT%\bin\mpc.pl"
  
  File "$%MADARA_ROOT%\bin\karl.exe"
  File "$%MADARA_ROOT%\bin\madara_version.exe"
  File "$%MADARA_ROOT%\bin\mpgen.exe"
  
  File "$%GAMS_ROOT%\bin\dynamic_simulation.exe"
  File "$%GAMS_ROOT%\bin\gams_controller.exe"
  
SectionEnd

Section "-basic" SEC08
  SetShellVarContext all
  
  SetOutPath "$INSTDIR"
  File /oname=$INSTDIR\ACE-VERSION.txt "$%ACE_ROOT%\VERSION"
  File /oname=$INSTDIR\MADARA-VERSION.txt "$%MADARA_ROOT%\VERSION.txt"
  File /oname=$INSTDIR\GAMS-VERSION.txt "$%GAMS_ROOT%\VERSION.txt"
  
  File /oname=$INSTDIR\ACE-LICENSE.txt "$%ACE_ROOT%\COPYING"
  File /oname=$INSTDIR\MADARA-LICENSE.txt "$%MADARA_ROOT%\LICENSE.txt"
  File /oname=$INSTDIR\GAMS-LICENSE.txt "$%GAMS_ROOT%\LICENSE.txt"
  
  File /oname=$INSTDIR\MADARA-README.txt "$%MADARA_ROOT%\README.txt"
  File /oname=$INSTDIR\GAMS-README.txt "$%GAMS_ROOT%\README.txt"
  
  CreateDirectory "$SMPROGRAMS\GAMS"
  CreateDirectory "$SMPROGRAMS\GAMS\ACE"
  CreateDirectory "$SMPROGRAMS\GAMS\MADARA"
  
  CreateShortCut "$SMPROGRAMS\GAMS\ACE\VERSION.lnk" "$INSTDIR\ACE-VERSION.txt"
  CreateShortCut "$SMPROGRAMS\GAMS\MADARA\VERSION.lnk" "$INSTDIR\MADARA-VERSION.txt"
  CreateShortCut "$SMPROGRAMS\GAMS\VERSION.lnk" "$INSTDIR\GAMS-VERSION.txt"
  
  CreateShortCut "$SMPROGRAMS\GAMS\ACE\LICENSE.lnk" "$INSTDIR\ACE-LICENSE.txt"
  CreateShortCut "$SMPROGRAMS\GAMS\MADARA\LICENSE.lnk" "$INSTDIR\MADARA-LICENSE.txt"
  CreateShortCut "$SMPROGRAMS\GAMS\LICENSE.lnk" "$INSTDIR\GAMS-LICENSE.txt"
  
  CreateShortCut "$SMPROGRAMS\GAMS\MADARA\README.lnk" "$INSTDIR\MADARA-README.txt"
  CreateShortCut "$SMPROGRAMS\GAMS\README.lnk" "$INSTDIR\GAMS-README.txt"
  
SectionEnd

Section -AdditionalIcons
  SetShellVarContext all

  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  WriteIniStr "$INSTDIR\MADARA.url" "InternetShortcut" "URL" "${MADARA_WEB_SITE}"
  WriteIniStr "$INSTDIR\ACE.url" "InternetShortcut" "URL" "${ACE_WEB_SITE}"
  
  CreateShortCut "$SMPROGRAMS\GAMS\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\GAMS\MADARA\Website.lnk" "$INSTDIR\MADARA.url"
  CreateShortCut "$SMPROGRAMS\GAMS\ACE\Website.lnk" "$INSTDIR\ACE.url"
  
  CreateShortCut "$SMPROGRAMS\GAMS\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Pre
  
  ; Ensure that only one installer is running for GAMS
  System::Call 'kernel32::CreateMutexA(i 0, i 0, t "GamsInstallMutex") i .r1 ?e'
  Pop $R0
 
  StrCmp $R0 0 +3
  MessageBox MB_OK|MB_ICONEXCLAMATION "The GAMS installer is already running."
  Abort

SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\bin\madara_version.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"

  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "OldMadaraRoot" "$%MADARA_ROOT%"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "OldGamsRoot" "$%GAMS_ROOT%"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "OldAceRoot" "$%ACE_ROOT%"

; set environment variables
   ; include for some of the windows messages defines
   ; HKLM (all users) vs HKCU (current user) defines
   ; set variable
   WriteRegExpandStr ${env_hkcu} GAMS_ROOT $INSTDIR
   WriteRegExpandStr ${env_hkcu} ACE_ROOT $INSTDIR
   WriteRegExpandStr ${env_hkcu} MADARA_ROOT $INSTDIR
   
   ; read the path variable
   ReadRegStr $1 ${env_hkcu} PATH

   ; remove references to GAMS in the PATH variable
   ${WordAdd} $1 ";" "+%GAMS_ROOT%\lib;%GAMS_ROOT%\bin" $2
   
   ; now add them back
   WriteRegExpandStr ${env_hkcu} PATH $2

   ; make sure windows knows about the change
   SendMessage ${HWND_BROADCAST} ${WM_WININICHANGE} 0 "STR:Environment" /TIMEOUT=5000

SectionEnd

; Section descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC01} "GAMS libraries compiled in debug mode (optional)"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC02} "GAMS libraries compiled in release mode (required)"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC04} "Tests for GAMS and MADARA"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC05} "Helpful scripts for GAMS projects"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC07} "Include files for GAMS, MADARA and ACE projects"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC08} "Version, License, etc."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC11} "Visual Studio 2013 Redistributable."
!insertmacro MUI_FUNCTION_DESCRIPTION_END


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "GAMS was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove GAMS and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall

  SetShellVarContext all
  RMDir /r "$SMPROGRAMS\GAMS"
  RMDir /r "$INSTDIR"

  ReadRegStr $0 ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "OldGamsRoot"
  ReadRegStr $3 ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "OldMadaraRoot"
  ReadRegStr $4 ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "OldAceRoot"

  StrCmp $0 '' 0 revert
   ; delete variable
   DeleteRegValue ${env_hkcu} ACE_ROOT
   DeleteRegValue ${env_hkcu} MADARA_ROOT
   DeleteRegValue ${env_hkcu} GAMS_ROOT
   ReadRegStr $1 ${env_hkcu} PATH
   
   ; remove references to GAMS in the PATH variable
   ${WordAdd} $1 ";" "-%GAMS_ROOT%\lib;%GAMS_ROOT%\bin" $2

   WriteRegExpandStr ${env_hkcu} PATH $2
   
   Goto done
   ; revert to the old GAMS, MADARA and ACE variables
   revert:
   WriteRegExpandStr ${env_hkcu} GAMS_ROOT $0
   WriteRegExpandStr ${env_hkcu} MADARA_ROOT $3
   WriteRegExpandStr ${env_hkcu} ACE_ROOT $4
   ; make sure windows knows about the change

   SendMessage ${HWND_BROADCAST} ${WM_WININICHANGE} 0 "STR:Environment" /TIMEOUT=5000
   done:
  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  SetAutoClose true
SectionEnd